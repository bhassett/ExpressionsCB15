<?xml version="1.0" standalone="yes"?>
<package version="2.1" displayname="Home Intro" includeentityhelper="true" debug="false">

  <!-- ###################################################################################################### -->
  <!-- Licensed by Interprise Solutions.					                -->
  <!-- http://www.InterpriseSolutions.com														                -->
  <!-- For details on this license please visit  the product homepage at the URL above.		                -->
  <!-- THE ABOVE NOTICE MUST REMAIN INTACT.                                                                   -->
  <!-- ###################################################################################################### -->

  <PackageTransform>
    <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:ise="urn:ise" exclude-result-prefixes="ise">
      <xsl:output method="html" omit-xml-declaration="yes" />
      
      <xsl:param name="CategoryID">
        <xsl:choose>
          <xsl:when test="/root/System/PageName = 'showmanufacturer.aspx' or /root/System/PageName = 'showsection.aspx'">0</xsl:when>
          <xsl:when test="/root/System/PageName = 'showcategory.aspx' and boolean(/root/QueryString/categoryid)">
            <xsl:value-of select="/root/QueryString/categoryid" />
          </xsl:when>
          <xsl:when test="(/root/System/PageName = 'showcategory.aspx' or /root/System/PageName = 'showproduct.aspx') and boolean(/root/Cookies/lastviewedentityinstanceid) and /root/Cookies/lastviewedentityname = 'Category'">
            <xsl:value-of select="/root/Cookies/lastviewedentityinstanceid" />
          </xsl:when>
          <xsl:otherwise>0</xsl:otherwise>
        </xsl:choose>
      </xsl:param>

      <xsl:template match="/">

        

        <div class=" panel-group" id="homeMenu">
          
          <!-- categories -->
          <div class="panel panel-default">
            <div class="panel-heading"  data-toggle="collapse" data-parent="#homeMenu" href="#categoryContent">
              <h4 class="panel-title">
                Browse Categories
                <i class="indicator glyphicon glyphicon-chevron-down  pull-right"></i>
              </h4>
            </div>
            <div id="categoryContent" class="panel-collapse collapse">
              <ul>
                <xsl:apply-templates select="/root/EntityHelpers/Category/Entity">
                  <xsl:with-param name="ePrefix" select="'c'" />
                </xsl:apply-templates>
              </ul>
            </div>
          </div>
          
          <!-- departments -->
          <div class="panel panel-default">
            <div class="panel-heading" data-toggle="collapse" data-parent="#homeMenu" href="#departmentContent">
              <h4 class="panel-title">
                Browse Departments
                <i class="indicator glyphicon glyphicon-chevron-down pull-right"></i>
              </h4>
            </div>
            <div id="departmentContent" class="panel-collapse collapse">
              <ul>
                <xsl:apply-templates select="/root/EntityHelpers/Department/Entity">
                  <xsl:with-param name="ePrefix" select="'d'" />
                </xsl:apply-templates>
              </ul>
            </div>
          </div>
          
          <!-- manufacturers -->
          <div class="panel panel-default">
            <div class="panel-heading" data-toggle="collapse" data-parent="#homeMenu" href="#manufacturerContent">
              <h4 class="panel-title">
                Browse Manufacturers
                <i class="indicator glyphicon glyphicon-chevron-down  pull-right"></i>
              </h4>
            </div>
            <div id="manufacturerContent" class="panel-collapse collapse">
              <ul>
                <xsl:apply-templates select="/root/EntityHelpers/Manufacturer/Entity">
                  <xsl:with-param name="ePrefix" select="'m'" />
                </xsl:apply-templates>
              </ul>
            </div>
          </div>
        </div>

        <script>
          function toggleChevron(e) { $(e.target).prev('.panel-heading').find("i.indicator").toggleClass('glyphicon-chevron-down glyphicon-chevron-up'); }
          $('#homeMenu').on('hidden.bs.collapse', toggleChevron);
          $('#homeMenu').on('shown.bs.collapse', toggleChevron);
        </script>

      </xsl:template>

      <xsl:template match="Entity">
        <xsl:param name="ePrefix"></xsl:param>
        <xsl:param name="eName" select="ise:GetMLValue(Description)" />
        <xsl:param name="eID" select="concat($ePrefix,EntityID)"></xsl:param>
        
        <li>
          <a>
            <xsl:choose>
              <xsl:when test="count(child::Entity) > 0">
                <xsl:attribute name="data-toggle">collapse</xsl:attribute>
                <xsl:attribute name="class">panel-heading</xsl:attribute>
                <xsl:attribute name="href">
                  <xsl:value-of select="concat('#', $eID)"></xsl:value-of>
                </xsl:attribute>
                <i class="indicator glyphicon glyphicon-chevron-down pull-right"></i>
              </xsl:when>
              <xsl:otherwise>
                <xsl:attribute name="href">
                  <xsl:value-of select="concat($ePrefix, '-',EntityID,'-',ise:FormatStringForLink(SEName),'.aspx')"></xsl:value-of>
                </xsl:attribute>
              </xsl:otherwise>
            </xsl:choose>
            <xsl:value-of select="$eName" />
          </a>

          <!-- recursive call to build child nodes -->
          <xsl:if test ="count(child::Entity) > 0">
            <ul class="panel-collapse collapse" id="{$eID}">
              <xsl:apply-templates select="Entity">
                <xsl:with-param name="ePrefix" select="$ePrefix" />
              </xsl:apply-templates>
            </ul>
          </xsl:if>
        </li>
      </xsl:template>
      
      

    </xsl:stylesheet>
  </PackageTransform>
</package>