<?xml version="1.0" standalone="yes"?>
<!-- ###################################################################################################### -->
<!-- Licensed by Interprise Solutions.					                -->
<!-- http://www.InterpriseSolutions.com														                -->
<!-- For details on this license please visit  the product homepage at the URL above.		                -->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.                                                                   -->
<!-- ###################################################################################################### -->
<package version="2.1" displayname="Categories" debug="false" includeentityhelper="true">
	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:ise="urn:ise" exclude-result-prefixes="ise">
			<xsl:output method="html" omit-xml-declaration="yes" />

			<xsl:param name="CategoryID">
				<xsl:choose>
					<xsl:when test="/root/System/PageName = 'showmanufacturer.aspx' or /root/System/PageName = 'showsection.aspx'">0</xsl:when>
					<xsl:when test="/root/System/PageName = 'showcategory.aspx' and boolean(/root/QueryString/categoryid)">
						<xsl:value-of select="/root/QueryString/categoryid" />
					</xsl:when>
					<xsl:when test="(/root/System/PageName = 'showcategory.aspx' or /root/System/PageName = 'showproduct.aspx') and boolean(/root/Cookies/lastviewedentityinstanceid) and /root/Cookies/lastviewedentityname = 'Category'">
						<xsl:value-of select="/root/Cookies/lastviewedentityinstanceid" />
					</xsl:when>
					<xsl:otherwise>0</xsl:otherwise>
				</xsl:choose>
			</xsl:param>

			<xsl:param name="AncestorID">
				<xsl:for-each select="/root/EntityHelpers/Category//Entity[EntityID = $CategoryID]">
					<xsl:value-of select="ancestor::*/EntityID" />
				</xsl:for-each>
			</xsl:param>

			<xsl:param name="ParentID">
				<xsl:for-each select="/root/EntityHelpers/Category//Entity[EntityID = $CategoryID]">
					<xsl:value-of select="parent::*/EntityID" />
				</xsl:for-each>
			</xsl:param>

			<xsl:template match="/">
        <ul class="tame" >
					<xsl:apply-templates select="/root/EntityHelpers/Category/Entity">
						<xsl:with-param name="prefix" select="''" />
					</xsl:apply-templates>
        </ul>
			</xsl:template>

			<xsl:template match="Entity">
				<xsl:param name="prefix"></xsl:param>
				<xsl:param name="eName" select="ise:GetMLValue(Description)" />
                <xsl:variable name="smallcase" select="'abcdefghijklmnopqrstuvwxyz'" />
                <xsl:variable name="uppercase" select="'ABCDEFGHIJKLMNOPQRSTUVWXYZ'" />

				<li class="tame_plus">

          <div>
            <xsl:attribute name="class">
              <xsl:choose>
                <xsl:when test="count(child::Entity)&gt;0">
                  <xsl:choose>
                    <xsl:when test="(EntityID = $CategoryID or descendant::Entity/EntityID = $CategoryID)">
                      <![CDATA[collapsable]]>
                    </xsl:when>
                    <xsl:otherwise>
                      <![CDATA[expandable]]>
                    </xsl:otherwise>
                  </xsl:choose>
                </xsl:when>
                <xsl:otherwise >
                  <![CDATA[expandable tame_noimage]]>
                </xsl:otherwise>
              </xsl:choose>
            </xsl:attribute>
          </div>

          <xsl:value-of select="$prefix" />
             <xsl:choose>
                 <xsl:when test="translate(OpenInNewTab, $uppercase, $smallcase) = 'true'">
                      <a class="leftmenu" href="{VirtualPageValueExternalPage}" target ="_blank">
                             <xsl:if test="EntityID = $CategoryID or descendant::Entity/EntityID = $CategoryID">
                                    <xsl:attribute name="style">font-weight:bold</xsl:attribute>
                             </xsl:if>
                                  <xsl:value-of select="$eName" />
                       </a>
                  </xsl:when>
                  <xsl:otherwise>
                        <a class="leftmenu" href="{concat('c-',EntityID,'-',ise:FormatStringForLink(SEName),'.aspx')}">
                             <xsl:if test="EntityID = $CategoryID or descendant::Entity/EntityID = $CategoryID">
                                    <xsl:attribute name="style">font-weight:bold</xsl:attribute>
                             </xsl:if>
                                    <xsl:value-of select="$eName" />
                        </a>
                  </xsl:otherwise>
              </xsl:choose>
          
					<xsl:if test="count(child::Entity)&gt;0 and (EntityID = $CategoryID or descendant::Entity/EntityID = $CategoryID)">
						<ul class="tame" >
              <xsl:apply-templates select="Entity">
								<xsl:with-param name="prefix" select="concat($prefix, '&#160;&#160;')" />
							</xsl:apply-templates>
						</ul>
					</xsl:if>
				</li>
			</xsl:template>
      
		</xsl:stylesheet>
	</PackageTransform>
</package>